// ============================================================================
// (C) Copyright Schalk W. Cronje 2012 - 2013
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
// ============================================================================
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-cobertura-plugin:1.1.0'
    }
}

apply plugin : 'groovy'
apply plugin : 'bintray-publish'

group = 'org.ysb33r.groovy' 
archivesBaseName = 'groovy-vfs'

if (org.gradle.internal.SystemProperties.javaVersion.startsWith('1.6.')) {
    ext {
        BUILD_FOR_JAVA6=true
    }    
}

if (project.properties.BUILD_FOR_JAVA6) {
    apply plugin : 'cobertura'
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    version = version + '-java6'
    
    cobertura {
        format = 'xml'
        includes = ['main/**/*.java', 'main/**/*.groovy']
    }
} else {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}


jar {
    manifest {
        attributes 'Implementation-Title': 'Groovy VFS', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
    mavenRepo( url:'https://oss.jfrog.org/artifactory/repo' )
}   

dependencies {
    if (project.properties.BUILD_FOR_JAVA6) {
        compile group: 'org.codehaus.groovy',   name: 'groovy-all', version: '2.1+'
    } else {
        compile group: 'org.codehaus.groovy',   name: 'groovy-all', version: '2.1+', classifier: 'indy'
    }
    testCompile group: 'com.jcraft',            name: 'jsch', version:'0.1.48'
	
 /*
	Commons Collections Version 3.1 	LRU Cache (optional)
	Apache Jackrabbit Version 1.5.2 or later 	WebDAV
	jCIFS Version 0.8.3 or later. 	CIFS (VFS sandbox)
	javamail Version 1.4
*/	
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    
}

test {
    systemProperties TESTFSREADROOT : "${projectDir}"   
    systemProperties "ratpack.port" : 9999
}

uploadArchives {
    repositories {
        bintrayMavenDeployer {
            username    project.properties.bintrayUserName
            apiKey      project.properties.bintrayApiKey
            repoOwner   'ysb33r'
            repoName    'grysb33r'
            packageName 'groovy-vfs'
            description 'A DSL for Groovy that wraps Apache VFS '
            tags        'groovy','vfs','dsl'
        }
    }
}


